/*
 * Copyright 2019-2021 Daniel Ochieng' Olanga.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'maven-publish'
    id 'org.kordamp.gradle.project' version '0.42.1'
}

ext {
    githubUsername  = project.hasProperty('githubUsername') ? project.property('githubUsername') : System.getenv('GITHUB_USERNAME')
    githubPassword  = project.hasProperty('githubToken') ? project.property('githubToken') : System.getenv('GITHUB_TOKEN')
}

config {
    release = (rootProject.findProperty('release') ?: false).toBoolean()

    info {
        name        = rootProject.name
        vendor      = 'Mpesa4j'
        description = project.project_description
        inceptionYear = '2019'
        tags          = ['mpesa', 'daraja', 'java', 'sdk']

        links {
            website      = project.project_website
            issueTracker = project.project_issues
            scm          = project.project_repo
        }

        scm {
            url                     = project.project_website
            connection              = project.project_scm
            developerConnection     = project.project_scm
        }

        issueManagement {
            system  = project.project_issuemanagement_type
            url     = project.project_issues
        }

        specification {
            enabled = false
        }

        implementation {
            enabled = false
        }

        people {
            person {
                id    = project.project_developer_id
                name  = project.project_developer_name
                email = project.project_developer_email
                url   = project.project_developer_url
                roles = ['author', 'developer']
            }
        }
    }

    docs {
        groovydoc {
            enabled = false
        }

        javadoc {
            enabled = true
        }
    }

    licensing {
        licenses {
            license {
                id = 'Apache-2.0'
                ext.year = Calendar.getInstance().get(Calendar.YEAR)
            }
        }
    }
}

allprojects {
    apply plugin: 'idea'

    repositories {
        jcenter()
        gradlePluginPortal()
    }
}

subprojects {
    if (project.name.startsWith('mpesa4j')) {
        publishing {
            repositories {
                maven {
                    name = "GitHubPackages"
                    url = uri("https://maven.pkg.github.com/ochiengolanga/mpesa4j")
                    credentials {
                        username = project.githubUsername
                        password = project.githubPassword
                    }
                }
            }
        }

        if (project.name.equalsIgnoreCase('mpesa4j')) {
            config {
                info {
                    name        = 'mpesa4j'
                    description = 'The unofficial Safaricom\'s MPesa Daraja 1.0 API Java integration library'
                }
            }
        } else if (project.name.equalsIgnoreCase('mpesa4j-spring-boot')) {
            config {
                info {
                    name        = 'mpesa4j-spring-boot'
                    description = 'The unofficial MPesa4j\'s Spring boot'
                    tags        = ['mpesa', 'daraja', 'java', 'sdk', 'spring', 'spring-boot']
                }
                licensing {
                    enabled = false
                }
            }
        }
    }
}